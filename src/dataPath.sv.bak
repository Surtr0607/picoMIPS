module dataPath
    #(
        parameter n = 8,        // Data bus width
        parameter A_SIZE = 3,   // ALU function width
        parameter R_SIZE = 3    // GPR address width
    )
    (
        // Outputs
        // Demo
        output wire [(n-1):0] displayResult,

        // Inputs
        // Demo
        input wire [9:0] SW,
        // Control
        input [1:0] ALUfunc,
        input wire imm,
        input wire immswitches,
        // Data
        input wire [(R_SIZE-1):0] Raddr1,
        input wire [(n-1):0] Raddr2,

        // Clock/reset
        input wire clk
    );


    // GPR data out
    wire [(n-1):0] Rdata1, Rdata2;

    // ALU IO
    wire [(n-1):0] aluA, aluB, result;
    assign aluA = Rdata1;
    assign aluB = imm ? (immswitches ? SW[(n-1):0] : Raddr2) : Rdata2;

    // Demo
    assign displayResult = result;


    // GPR instance
    regs
        #(
            .n(n),
            .R_SIZE(R_SIZE)
        ) re
        (
            // Outputs
            .Rdata1(Rdata1),
            .Rdata2(Rdata2),

            // Inputs
            .Wdata(result),
            .Raddr1(Raddr1),
            .Raddr2(Raddr2[(R_SIZE-1):0]),

            // Clock
            .clk(clk)
        );


    // ALU instance
    alu
        #(
            .n(n)
        ) al
        (
            // Outputs
            .result(result),

            // Inputs
            .a(aluA),
            .b(aluB),
            .ALUfunc(ALUfunc)
        );

endmodule